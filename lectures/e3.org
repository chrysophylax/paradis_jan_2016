#+STARTUP: overview, hideblocks
#+BEGIN_kv
title: Modules and Functions
subtitle: Lecture E3 
author: Joe Armstrong
copyright: \copyright 2016 - 2019 -- Joe Armstrong
email: joearms@gmail.com
affiliation: KTH, Whoomph Software AB
process: orgmode_plugin_slides
#+END_kv
* Topics
** The startup file, paths.
** Ways to run your program.
** Expressions.
** Higher order functions.
** Von-Neumann Bottleneck.

* The startup file

** The file {\sl hidden} file \verb+.erlang+ is consulted when you start Erlang.
** This can be in the directory where Erlang was started or your home directory.
** This file can contain any Erlang commands. They are executed when you start Erlang.

* Search Paths

** Code is dynamically loaded at run time.
** \verb+code:get_path()+ tells you want the current path is.
** The first time you call \verb+foo:+ the system will search for a file called \verb+foo.beam+ using the current search path.
** use \verb+code:add_patha(Dir)+ or \verb+code:add_pathz(Dir)+ in your startup file.
   
* Compiling your program

** In the Erlang shell (\verb+> c(FileName).+)
** In the OS command shell (\verb+$erlc foo.erl+).
** From a Makefile.
** from rebar.
   
* Running Erlang from the shell

#+BEGIN_shell
$ erl -pa Path1 -pa Path2 -s M F Arg1 Arg2 ...
#+END_shell

** Can use noshell to stop the prompt

\verb+$erl -noshell ...+

* Running Erlang from a shell script

Create a file \verb+runme+ like this:

\begin{Verbatim}[frame=single]
#!/bin/bash
$ erl -pa Path1 -pa Path2 -s M F $1 ...
\end{Verbatim}

Then run it:

\begin{verbatim}
$ chmod u+x runme
$ runme arg1 ...
\end{verbatim}

* Running Erlang from escript
#+BEGIN_erlang
#!/usr/bin/env escript

main(Args) ->
    io:format("Script started with args:~p~n",[Args]),
    [A] = Args,
    N = list_to_integer(A),
    K = fac(N),
    io:format("fac(~p) = ~p~n",[K, N]).

fac(0) -> 1;
fac(N) -> N * fac(N-1).
#+END_erlang

* chmod the file then you can run it as a script.

#+BEGIN_shell
$ chmod a+x fac.esh 
$ ./fac.esh 123
Script started with args:["123"]
fac(1214630436702532967576624324188129585545421708848338
23153289181618292358923621676688311569606126402021707358
35221294047782591091570411651472186029519906261646730733
907419814952960000000000000000000000000000) = 123
#+END_shell

* case expression
#+BEGIN_erlang
area(A) ->
    case A of
       {square, X} ->
           X*X;
       {rectangle, X, Y} ->
           X*Y
       ...
    end
#+END_erlang   

Or:

#+BEGIN_erlang
area({square,X}) -> X*X;
area({rectangle,X,Y}) -> X*Y.
#+END_erlang   

* if expression
#+BEGIN_erlang
weekend(Day) ->
    if
       Day == saturday -> 
          true;
       Day == sunday -> 
          true;
       true ->
          false
    end.
#+END_erlang   
  
** if is an \textcolor{red}{expression} and not an statement.
* Higher Order Functions

** Define own control abstractions.
** Functions as arguments.
** Function in Messages.
** Functions that return functions.
** Lamdba Abstractions.
** Von Nuemann Bottleneck.

* Fun Syntax
Creating:
#+BEGIN_erlang
F = fun(X,Y,...) -> ... end
#+END_erlang   
      
Using:
#+BEGIN_shell
1> D = fun(X) -> 2*X end.
#Fun<erl_eval.6.17052888>
2> hofs:for(1,10,D).
[2,4,6,8,10,12,14,16,18,20]
3> 
#+END_shell
** To refer to a fun we can say \verb+fun NameOfFun/Arity+.

* Functions as arguments

#+BEGIN_erlang
for(Max,Max,F) -> [F(Max)];
for(I, Max, F) -> [F(I) | for(I+1,Max,F)].
#+END_erlang   
      
#+BEGIN_shell
1> D = fun(X) -> 2*X end.
#Fun<erl_eval.6.17052888>
2> hofs:for(1,10,D).
[2,4,6,8,10,12,14,16,18,20]
3> 
#+END_shell

* Function which return Funs
#+BEGIN_erlang
make_adder(Y) ->
    fun(X) -> X + Y end.
#+END_erlang

#+BEGIN_shell
> Adder10 = hofs:make_adder(10).
#Fun<hofs.7.134015048>
> Adder10(10).
20
#+END_shell


* Funs capture variables in their environment
#+BEGIN_erlang
%% hofs.erl
test2() ->
    Y = 10,  
    Adder = fun(X) -> X + Y end,
    for(1,10,Adder).
#+END_erlang

* Funs retain captured variables after they have returned


#+BEGIN_erlang
test4() ->
    Y = 10,
    fun(X) -> X + Y end.
#+END_erlang

After \verb+test4+ has returned the value of the internal variable
\verb+Y+ is still retained. {\sl This is called a closure and is
incredably
useful}.


#+BEGIN_shell
> F=hofs:test4().  
#Fun<hofs.8.99099555>
> F(10).
20
#+END_shell



* Functions in messages
#+BEGIN_erlang
wait() ->
    receive
	F ->
	    receive
		{From, X} ->
		    From ! {self(), F(X)}
	    end
    end.
#+END_erlang


* Pattern matching in Funcs
#+BEGIN_shell
    [1,2,1] = map(fun(a) -> 1;
		     (b) -> 2
		  end, [a,b,a]),
#+END_shell

* Self-reference in Funs
#+BEGIN_shell
[1,2,6] = map(fun Fac(0) -> 1;
		  Fac(N) -> N * Fac(N-1)
	      end, [1,2,3]),
#+END_shell

* Lambda abstraction
** See the examples in \verb+lambda.erl+.

* Von-Neumann Bottleneck

** Word at-a-time programming:
A single line of code describes what happens to one data item
in this case a list cell:
#+BEGIN_erlang
double([]) -> [];
double([H|T]) -> [2*H|double(T)].
#+END_erlang

** List at-a-time programming

A single line of code describes what happens to a large number
of data items:

#+BEGIN_erlang
double(fun(X) -> 2*X end, L).
#+END_erlang

The Von Neumann bottleneck is caused by the memory bandwidth problem.
There is a large data memory, but the CPU memory has a very small
number of registers through which all data must pass.


* Simple list recursion
#+BEGIN_erlang
double([]) -> [];
double([H|T]) -> [2*H|double(T)].
#+END_erlang   

#+BEGIN_shell
3> c(math1).
{ok,math1}
4> math1:double([1,2,3,4,5]).
[2,4,6,8,10]
#+END_shell

* Accumulators

#+BEGIN_erlang
sum(L) -> sum_helper(L, 0).

sum_helper([], N) -> N;
sum_helper([H|T], N) ->
    N1 = N + H,
    sum_helper(T, N1).
#+END_erlang   
   
#+BEGIN_shell
1> math1:sum([1,2,3,4,5]).
15
#+END_shell

* sum\_and\_double

** travserses the list twice

#+BEGIN_erlang
sum_and_double(L) ->
    Sum = sum(L),
    Double = double(L),
    {Sum, Double}.

double([]) -> [];
double([H|T]) -> [2*H|double(T)].

sum([]) -> 0;
sum([H|T]) -> H + sum(T).
#+END_erlang   
   
#+BEGIN_shell
8> lists1:sum_and_double([1,2,3,4]). 
{10,[2,4,6,8]}
#+END_shell

* sum\_and\_double1

** traverses the list once -- \textcolor{Red}{gets the answer wrong} 

#+BEGIN_erlang
sum_and_double1(L) -> 
    sum_and_double_helper1(L, [], 0).

sum_and_double_helper1([H|T], L, N) ->
    sum_and_double_helper1(T, [2*H|L], N + H);
sum_and_double_helper1([], L, N) ->
    {N, L}.
#+END_erlang   
   
#+BEGIN_shell
8> lists1:sum_and_double1([1,2,3,4]). 
{10,[8,6,4,2]}
#+END_shell

* sum\_and\_double2

** traverses the list once -- \textcolor{Red}{gets the answer right} 

#+BEGIN_erlang
sum_and_double2(L) -> 
    sum_and_double_helper2(L, [], 0).

sum_and_double_helper2([H|T], L, N) ->
    sum_and_double_helper2(T, [2*H|L], N + H);
sum_and_double_helper2([], L, N) ->
    {N, lists:reverse(L)}.
#+END_erlang   
   
#+BEGIN_shell
8> lists1:sum_and_double2([1,2,3,4]). 
{10,[2,4,6,8]}
#+END_shell
   
* sum\_and\_double3

** Renamed the helper function.
   
#+BEGIN_erlang
sum_and_double3(L) -> 
    sum_and_double3(L, [], 0).

sum_and_double3([H|T], L, N) ->
    sum_and_double3(T, [2*H|L], N + H);
sum_and_double3([], L, N) ->
    {N, lists:reverse(L)}.
#+END_erlang   
   
#+BEGIN_shell
1> lists1:sum_and_double2([1,2,3,4]). 
{10,[2,4,6,8]}
#+END_shell

* Natural order in lists

** Write the code using accumulators.
** Don't bother if the lists come out in the wrong order.
** Reverse the order at the end.
** \verb+lists:reverse+ is a BIF not a function call \textcolor{red}{but it looks like a function call}.
* Tail Recursion
** The last thing you do is call another routine
** really ``last call optimization''

\begin{Verbatim}
function a(){
   call x
   call y
}

call x is compiled as:
  push address of y
  call x

the call to y (a lastcall) is compiled as
  jmp y
\end{Verbatim}

* BIFS
** Do things that are impossible in erlang ( \verb+atom_to_list(abc)+).
** Do things that are slow in erlang ( \verb+lists:reverse/1+).
** Look like erlang function calls.

* Apply
  
** \verb+apply(Mod, Func, [Arg1, Arg2, ..., ArgN])+ -- same as \verb+Mod:Func(Arg1, Arg2, ..., ArgN)+

#+BEGIN_shell
1> apply(lists, reverse, [[1,2,3,4]]).
[4,3,2,1]
#+END_shell

* Guards
#+BEGIN_erlang
upcase(X) when $a =< X andalso X =< $z ->
    X -$a + $A;
upcase(X) ->
    X.
#+END_erlang   
   
#+BEGIN_shell
1> c(lists1).
{ok,lists1}
2> lists1:upcase($a).
65
3> $a.
97
#+END_shell

* List Comprehensions
** \verb+[ Expression || Pattern <- List]+

#+BEGIN_shell
1> [lists1:upcase(I) || I <- "hello"].
"HELLO"
2> [{X,Y} || X <- [1,2,3], Y <- [a,b,c]]. 
[{1,a},{1,b},{1,c},
 {2,a},{2,b},{2,c},
 {3,a},{3,b},{3,c}]
#+END_shell

* Tuple programs
** Store tree like data structures in tuple
** XML

The XML \verb+<tag1 a1="abc" a2="def"> ... </tag1>+ can be represented
as \verb+{tag1, [{a1,"abc"},{a2,"def"}], [ ...]}+

#+BEGIN_erlang
path_search([Tag|T],[{Tag,_,Children}|_]) ->
    path_search(T, Children);
...
#+END_erlang

* Records
** What do the elements in a tuple mean?

#+BEGIN_erlang
X = {person,"zabdog","polgelzipper",42,22,....}
#+END_erlang

#+BEGIN_erlang
-record(person,
   {firstname, lastname, footsize, age,
    ....}).

X = #person{age=20, footsize=10}

birthday(X) ->
     Age = X#person.age,
     X#person{age=Age+1}.
#+END_erlang

* Maps
#+BEGIN_shell
1> X = #{first => "Rip", age => 2345}.  %% create
#{age => 2345,first => "Rip"}
2> #{age := A} = X.          %% match
#{age => 2345,first => "Rip"}
3> A.
2345
4> X1 = X#{age := 222}.   %% update existing key
#{age => 222,first => "Rip"}
5> X2 = X#{sex := mail}.   %% update new key fails
** exception error: {badkey,sex}
6> X2 = X#{sex => mail}.   %% update new key works
#{age => 2345,first => "Rip",sex => mail}
#+END_shell 
* Catch
#+BEGIN_shell
1> X = atom_to_list(1).
** exception error: bad argument
     in function  atom_to_list/1
        called as atom_to_list(1)
2> X.
 1: variable 'X' is unbound
3> X = (catch atom_to_list(1)).
{'EXIT',{badarg,
    [{erlang,atom_to_list,[1],[]},
     {erl_eval,do_apply,6,
       [{file,"erl_eval.erl"},{line,573}]},
          ...
#+END_shell

* Try ... catch, catch .. throw
#+BEGIN_erlang
try F() 
catch
   exit:... ->
   throw:... ->
   error:... ->
after
   ...
end
#+END_erlang
  
* ++  and --\,--

** X \verb|++| Y is an infix notation for \verb+lists:append(X,Y)+
** Takes time \verb+O(length(X)).+

#+BEGIN_shell
> "abc" ++ "123".
"abc123"
> "abc123" -- "abc".
"123"
> "abc123" -- "123".
"abc"
> "abc123xyz" -- "123".
"abcxyz"
> "abc12xyz" -- "123". 
"abcxyz"
#+END_shell
